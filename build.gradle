buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'forge'

version = "1.0.0"
group = "com.dsh105.holoapi" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "HoloAPI-forge"

description = "Holograms in Minecraft!"

sourceCompatibility = 1.6
targetCompatibility = 1.6

minecraft {
    version = "1.7.2-10.12.0.1057"
    forgeVersion = "10.12.0.1057"

    replaceIn "com/dsh105/holoapi/forge/references/Reference.java"
    replace "%VERSION%", project.version

    assetDir = "eclipse/assets"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/forge/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/forge/resources'
        }
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version, 'mcversion': project.minecraft.version,
                'forgeVersion': project.minecraft.forgeVersion
        include 'mcmod.info'
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    appendix = 'universal'
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

//Create the jars. Located in /build/libs
//The *-universal-* is the right one!
artifacts {
    archives sourceJar
    archives deobfJar
}